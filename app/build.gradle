apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'

apply from: "../configuration.gradle"
apply from: "../dependencies.gradle"

android {

    compileSdkVersion sdk.compileSdkVersion
    buildToolsVersion sdk.buildToolsVersion

    defaultConfig {
        applicationId "br.com.rafael.pokedexgdgjf"
        minSdkVersion sdk.minSdkVersion
        targetSdkVersion sdk.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        releaseConfig {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEY_STORE_PATH)
            storePassword KEY_STORE_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_URL", "\"http://pokeapi.co/api/v2/\""
            signingConfig signingConfigs.releaseConfig
        }
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEV"
            buildConfigField "String", "API_URL", "\"http://pokeapi.co/api/v2/\""
        }
    }

    productFlavors {
        devLocal {
            applicationId = "br.com.rafael.pokedexgdgjf.devLocal"

            resValue "string", "app_name", "Pokedex Local"
        }
        dev {
            applicationId = "br.com.rafael.pokedexgdgjf.release"

            resValue "string", "app_name", "Pokedex GDG JF"
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release') && !variant.getFlavors().get(0).name.equals('dev')) {
            variant.setIgnore(true);
        }
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.properties'
        exclude 'META-INF/maven/com.google.dagger/dagger/pom.xml'
    }
}

dependencies {

    // Support Libraries
    compile deps.appCompat
    compile deps.designCompat
    compile deps.recyclerViewCompat
    compile deps.cardViewCompat
    compile deps.supportAnnotations

    // Rx Libraries
    compile deps.rxJava
    compile deps.rxAndroid
    compile deps.rxMath

    // Butter Knife
    compile deps.butterKnife
    apt deps.butterKnifeCompiler

    compile deps.dagger
    apt deps.daggerCompiler

    // Retrofit 2
    compile deps.retrofit2
    compile deps.gsonConverter
    compile deps.rxJavaAdapter

    // OkHttp
    //compile "com.squareup.okhttp:okhttp:$OKHTTP_VERSION"
    compile deps.logInterceptor
    compile deps.digest

    // Timber
    compile deps.timber

    // Circle Image View
    compile deps.circleImageView

    // Image Libraries
    compile deps.picasso
    compile deps.pollexor

    compile deps.parcelPlease
    apt deps.parcelPleaseProcessor

    debugCompile deps.leakCanary
    releaseCompile deps.leakCanary
    testCompile deps.leakCanary

    // Test libraries
    androidTestCompile deps.espresso
    androidTestCompile deps.runner
    androidTestCompile deps.supportAnnotations

    testCompile deps.jUnit
    androidTestCompile(deps.jUnit) {
        exclude module: 'hamcrest-core'
    }

    testCompile deps.mockito
    androidTestCompile deps.mockito
    compile deps.javaxInject

    testApt deps.dagger
}
